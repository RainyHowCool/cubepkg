grammar;

use cubepkg::Operation;

match {
    // Skip whitespace
    r"\s+" => {},
    // Skip comments
    r"//[^\n\r]*" => {},
    // and other do nothing changes
    _
}

pub Operations: Vec<Operation> = {
    <op:Operation> => vec![op],
    <mut ops:Operations> <op:Operation> => {
        ops.push(op);
        ops
    }
}

Operation: Operation = {
    OperationCmd
}

OperationCmd: Operation = {
    "imm" <reg:Regs> "," <int_const:IntConst> => Operation::new(1, reg, int_const),
    "add" <reg:Regs> "," <int_const:IntConst> => Operation::new(2, reg, int_const),
    "sub" <reg:Regs> "," <int_const:IntConst> => Operation::new(3, reg, int_const),
    "mul" <reg:Regs> "," <int_const:IntConst> => Operation::new(4, reg, int_const),
    "div" <reg:Regs> "," <int_const:IntConst> => Operation::new(5, reg, int_const),
    "addr" <reg1:Regs> "," <reg2:Regs> => Operation::new(6, reg1, reg2),
    "subr" <reg1:Regs> "," <reg2:Regs> => Operation::new(7, reg1, reg2),
    "mulr" <reg1:Regs> "," <reg2:Regs> => Operation::new(8, reg1, reg2),
    "divr" <reg1:Regs> "," <reg2:Regs> => Operation::new(9, reg1, reg2),
	"mov" <reg1:Regs> "," <reg2:Regs>  => Operation::new(0xA, reg1, reg2),
    "ldr" <reg:Regs> "," <mem_offest:IntConst> => Operation::new(0xB, reg, mem_offest),
    "str" <mem_offest:IntConst> "," <reg:Regs> => Operation::new(0xC, mem_offest, reg),
    "shr" <reg:Regs> "," <offest:IntConst> => Operation::new(0xD, reg, offest),
    "shl" <reg:Regs> "," <offest:IntConst> => Operation::new(0xE, reg, offest),
    "quit" => Operation::new(0xF0, 0, 0),
    "ucs" => Operation::new(0xF1, 0, 0),
    "uds" => Operation::new(0xF2, 0, 0),
}

Regs: i32 = {
    r"[rR][0-9]+" => {
        let s = <>.to_string();
        i32::from_str_radix(&s[1..], 10).unwrap()
    }
}

IntConst: i32 = {
    r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
    r"0[0-7]+" => i32::from_str_radix(&<>[1..], 8).unwrap(),
    r"0x[0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
    r"0X[0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}